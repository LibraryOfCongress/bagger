subprojects{
  apply plugin: 'java'
  apply plugin: 'maven'
  apply plugin: 'findbugs'
  apply plugin: 'pmd'
  apply from: '../eclipse.gradle'

  group = 'loc-repository'

  sourceCompatibility = 1.7
  targetCompatibility = 1.7

  repositories {
    maven { url "http://spring-rich-c.sourceforge.net/maven2repository" }
    jcenter()
    mavenCentral()
  }

  test { //show test output
    testLogging.showStandardStreams = true
  }

  findbugsTest.enabled = false

  findbugs {
    ignoreFailures = true //don't fail the gradle build if bugs are found
  }

  pmd {
    ruleSets = ["java-basic", "java-braces"]
    ignoreFailures = true //don't fail the gradle build if rules are broken
  }

  //sign all the bagger jars
  jar.doLast {
    ant.signjar(destDir: jar.destinationDir,
      jar: jar.archivePath, 
      alias:"rdc",
      keystore:"${rootDir.path}/keystore/bagger.ks",
      storepass:"bagger-rdc", 
      preservelastmodified:"true") 
  }
}
